buildscript {
    repositories {
        maven {
            name = 'forge'
            url = 'https://maven.minecraftforge.net'
        }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '[6.0,6.2)', changing: true
        classpath 'org.spongepowered:mixingradle:0.7.+'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

var mc_version = '1.20.1'
var forge_version = '47.2.0'
var modid = 'wpo'
group = 'net.skds.wpo'
version = '0.3.1'
archivesBaseName = "water_physics_overhaul-${mc_version}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'parchment', version: "2023.09.03-${mc_version}"
    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', modid
        }

        server {
            property 'forge.enabledGameTestNamespaces', modid
            args '--nogui'
        }
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', modid
        }

        data {
            workingDirectory project.file('run-data')
            args '--mod', modid, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    flatDir {
        dir 'libs'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    implementation fg.deobf(files("libs/skds_core-1.20.1-0.3.0.jar"))
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest {
        attributes([
            "MixinConfigs": "mixins.${modid}.json",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
        ])
    }
}

mixin {
    add sourceSets.main, "mixins.${modid}.refmap.json"
    config "mixins.${modid}.json"
}